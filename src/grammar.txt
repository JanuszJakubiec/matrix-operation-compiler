########################################################################
############################### Program ################################
########################################################################

program : instructions_opt

instructions_opt : instructions
                 | epsilon

instructions : instructions instruction
             | instruction

instruction : expression SEMICOLON
            | command SEMICOLON
            | conditional_statement
            | while_statement
            | for_statement
            | instruction_block

epsilon :

########################################################################
############################# Flow control #############################
########################################################################

conditional_statement : if_statement ELSE body
                      | if_statement %prec IF

if_statement : IF L_R_BRACKET sentence R_R_BRACKET body

while_statement : WHILE L_R_BRACKET sentence R_R_BRACKET body

for_statement : FOR ID ASSIGN range body

body : instruction

instruction_block : L_C_BRACKET instructions_opt R_C_BRACKET

range : expression COLON expression

########################################################################
#                               Sentence                               #
########################################################################

sentence : sentence OR sentence
         | sentence AND sentence
         | NOT sentence
         | expression EQUAL expression
         | expression NOT_EQUAL expression
         | expression GREATER expression
         | expression LESS expression
         | expression GREATER_EQUAL expression
         | expression LESS_EQUAL expression

########################################################################
#                              Expression                              #
########################################################################

expression : expression PLUS expression
           | expression MINUS expression
           | expression MAT_PLUS expression
           | expression MAT_MINUS expression
           | expression TIMES expression
           | expression DIVIDE expression
           | expression MAT_TIMES expression
           | expression MAT_DIVIDE expression
           | MINUS expression %prec UNARY_MINUS
           | expression TRANSPOSE
           | EYE L_R_BRACKET expression R_R_BRACKET
           | ZEROS L_R_BRACKET expression R_R_BRACKET
           | ONES L_R_BRACKET expression R_R_BRACKET
           | L_R_BRACKET expression R_R_BRACKET
           | assignment
           | changeable
           | INTEGER
           | FLOAT
           | STRING

assignment : changeable ASSIGN expression
           | changeable PLUS_ASSIGN expression
           | changeable MINUS_ASSIGN expression
           | changeable TIMES_ASSIGN expression
           | changeable DIVIDE_ASSIGN expression

changeable : ID
           | matrix
           | expression L_S_BRACKET number COMMA number R_S_BRACKET %prec MAT_ELEMENT

########################################################################
#                               Command                                #
########################################################################

command : print_command
        | RETURN expression
        | CONTINUE
        | BREAK

print_command : PRINT sequence

########################################################################
#                               Matrix                                 #
########################################################################

matrix : L_S_BRACKET row_sequence R_S_BRACKET

row_sequence : row COMMA row_sequence
             | row

row : L_S_BRACKET sequence R_S_BRACKET

sequence : expression COMMA sequence
         | expression

